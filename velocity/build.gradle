plugins {
    id "com.github.johnrengelman.shadow" version "5.2.0"
    id "java"
    id "signing"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "io.github.lxgaming"
archivesBaseName = "ticket-velocity"
version = version

configurations {
    provided {
        compile.extendsFrom(provided)
    }

    compileJava.dependsOn(":common:build")
    jar.enabled = false
    jar.dependsOn("shadowJar")
}

repositories {
    jcenter()
    maven {
        name = "lxgaming"
        url = "https://dl.bintray.com/lxgaming/maven"
    }
    maven {
        name = "velocity"
        url = "https://repo.velocitypowered.com/snapshots"
    }
}

dependencies {
    shadow(project(path: ":common", configuration: "shadow"))
    annotationProcessor("com.velocitypowered:velocity-api:1.0.0-SNAPSHOT")
    provided("com.velocitypowered:velocity-api:1.0.0-SNAPSHOT")
    provided("io.github.lxgaming:location-api:1.0.5")
    provided("io.github.lxgaming:redisvelocity-api:1.1.3")
}

jar {
    from {
        (configurations.compile - configurations.provided).findAll({
            it.isDirectory() || it.name.endsWith(".jar")
        }).collect({
            it.isDirectory() ? it : zipTree(it)
        })
    }
}

processResources {
    from("LICENSE")
    rename("LICENSE", "LICENSE-" + archivesBaseName)
}

shadowJar {
    classifier = null
    configurations = [project.configurations.shadow]
    relocate("com.github.benmanes.caffeine", "io.github.lxgaming.ticket.lib.caffeine")
    relocate("com.google.errorprone", "io.github.lxgaming.ticket.lib.errorprone")
    relocate("com.mysql", "io.github.lxgaming.ticket.lib.mysql")
    relocate("com.zaxxer.hikari", "io.github.lxgaming.ticket.lib.hikari")
    relocate("org.apache.commons.lang3", "io.github.lxgaming.ticket.lib.lang3")
    relocate("org.checkerframework", "io.github.lxgaming.ticket.lib.checkerframework")
    relocate("org.gjt", "io.github.lxgaming.ticket.lib.gjt")
}

task signJar {
    doFirst {
        if (!project.hasProperty("signing.keyStorePath") || !project.hasProperty("signing.secretKeyRingFile")) {
            project.logger.warn("========== [WARNING] ==========")
            project.logger.warn("")
            project.logger.warn("   This build is not signed!   ")
            project.logger.warn("")
            project.logger.warn("========== [WARNING] ==========")
            throw new StopExecutionException()
        }
    }

    doLast {
        configurations.archives.allArtifacts.files.each {
            ant.signjar(
                jar: it,
                alias: project.property("signing.alias"),
                storepass: project.property("signing.keyStorePassword"),
                keystore: project.property("signing.keyStorePath"),
                keypass: project.property("signing.keyStorePassword"),
                preservelastmodified: project.property("signing.preserveLastModified"),
                tsaurl: project.property("signing.timestampAuthority"),
                digestalg: project.property("signing.digestAlgorithm")
            )
            project.logger.lifecycle("JAR Signed: " + it.name)

            signing.sign(it)
            project.logger.lifecycle("PGP Signed: " + it.name)
        }
    }
}