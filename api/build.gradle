plugins {
    id "java-library"
    id "maven-publish"
    id "net.kyori.blossom"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

archivesBaseName = "ticket-api"

blossom {
    replaceToken("@version@", version)
}

configurations {
}

repositories {
}

dependencies {
    api("com.google.code.gson:gson:${gsonVersion}")
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    failOnError = false
}

processResources {
    from("../LICENSE")
    rename("LICENSE", "LICENSE-Ticket")
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            groupId group
            artifactId archivesBaseName
            version version
            pom {
                name = "Ticket"
                description = "Ticketing System for BungeeCord and Velocity"
                url = "https://github.com/LXGaming/Ticket"
                developers {
                    developer {
                        id = "lxgaming"
                        name = "LXGaming"
                    }
                }
                issueManagement {
                    system = "GitHub Issues"
                    url = "https://github.com/LXGaming/Ticket/issues"
                }
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/LXGaming/Ticket.git"
                    developerConnection = "scm:git:https://github.com/LXGaming/Ticket.git"
                    url = "https://github.com/LXGaming/Ticket"
                }
            }
        }
    }
    repositories {
        if (project.hasProperty("sonatype.username") && project.hasProperty("sonatype.password")) {
            maven {
                name = "sonatype"
                url = version.contains("-SNAPSHOT") ? "https://s01.oss.sonatype.org/content/repositories/snapshots" : "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
                credentials {
                    username project.property("sonatype.username")
                    password project.property("sonatype.password")
                }
            }
        }
    }
}

signing {
    sign publishing.publications
}